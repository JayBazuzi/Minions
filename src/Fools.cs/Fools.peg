@namespace Fools.cs
@classname FoolsPegParser
@using System.Linq
@using Fools.cs.AST
@members
{
	public Report report { get; set; }
	public Cursor initial_state { get; private set; }
}

program <object>
	= #STATE{ state["Indentation"] = 0; initial_state = state; } s:statements eof { s }

statements <IList<object>>
	= line*

line <object>
	= !eof empty_line* INDENTATION s:statement { s }

empty_line<object>
	= [ \t]* eol_not_eof { null }
	/ [ \t]+ eof { null }

block_header
	= [^:\r\n]*

block_delim
	= ":" __ eol

block_body <IList<object>>
	= INDENT s:(<IList<object>>
		pass_block
		/ line+
		) UNINDENT { s }

possible_block_body <IList<object>>
	= INDENT s:(<IList<object>>
		lines:possible_line+ empty_line* { lines }
		) UNINDENT { s }

possible_line <object>
	= !eof empty_line* INDENTATION s:[^\r\n]+ { s }

pass_block <IList<object>>
	= INDENTATION "pass" eol { new List<object>() }

statement <object>
	= &(block_header block_delim !possible_block_body) code:(rest_of_current_line eol rest_of_current_line) &{ report.implicitly_empty_block(state, code) } { null }
	/ s:simpleStatement eol { s }
	/ "if" __ n:name block_delim s:block_body
		{ new ConditionalStatement() { condition = n, body_when_true = s } }
	/ "def" __ n:name block_delim s:block_body
		{ new FunctionDefinition() { name = n, body = s } }
	/ s:rest_of_current_line &{ report.unrecognized_statement(state, s) } { null }

simpleStatement <object>
	= a:name "=" __ b:name { new AssignmentStatement() { l_value = a, expression = b } }

rest_of_current_line
	= l:[^\r\n]* { l.Flatten() }

name
	= n:([a-zA-Z] [a-zA-Z0-9_]*) __ { n }

__ = [ ]*

eol = (eol_not_eof / eof)

eol_not_eof = ("\r\n" / "\n\r" / "\r" / "\n")

comment = "//" [^\r\n]*

eof = !.

INDENTATION
	= indents:"\t"* (
		code:(" " [^\r\n]*) &{ report.indent_with_spaces_error(state, indents.Flatten() + code) } { String.Empty }
		/ &{ indents.Count == state["Indentation"] }
		/ (&{ indents.Count > state["Indentation"] } code:rest_of_current_line &{ report.indentation_error(state, state["Indentation"], indents.Count, indents.Flatten() + code) } { String.Empty }))

INDENT
	= #STATE{ state["Indentation"] += 1; }

UNINDENT
	= #STATE{ state["Indentation"] -= 1; }
